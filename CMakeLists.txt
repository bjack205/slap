cmake_minimum_required(VERSION 3.0.0)
list(APPEND CMAKE_MESSAGE_CONTEXT slap)
project(slap VERSION 0.2.0 LANGUAGES C CXX)

set(CMAKE_C_STANDARD 11)
set(CMAKE_C_STANDARD_REQUIRED ON)

# Include useful modules
list(APPEND CMAKE_MODULE_PATH ${PROJECT_SOURCE_DIR}/cmake)
include(CMakePrintHelpers)
include(GNUInstallDirs)
include(Functions)
include(FetchContent)

# Add CPM Dependency Manager
include(FindCPM)

# Set RPATH
#   This ensures any shared libraries generated by the project
#   can be found when installed
#   See 26.2.2 of Professional CMake
file(RELATIVE_PATH relDir
    ${CMAKE_CURRENT_BINARY_DIR}/${CMAKE_INSTALL_BINDIR}
    ${CMAKE_CURRENT_BINARY_DIR}/${CMAKE_INSTALL_LIBDIR}
    )
set(CMAKE_INSTALL_RPATH $ORIGIN $ORIGIN/${relDir})

##############################
# Options
##############################

# Handle default build type
set(SLAP_DEFAULT_BUILD_TYPE "Release")
if (NOT CMAKE_BUILD_TYPE AND NOT CMAKE_CONFIGURATION_TYPES)
  message(STATUS "No build type specified. Setting CMAKE_BUILD_TYPE to ${SLAP_DEFAULT_BUILD_TYPE}")
  set(CMAKE_BUILD_TYPE ${SLAP_DEFAULT_BUILD_TYPE} CACHE STRING "Choose the type of build." FORCE)
  set_property(CACHE CMAKE_BUILD_TYPE PROPERTY STRINGS "Debug" "Release" "MinSizeRel" "RelWithDebInfo")
endif ()

# Enable testing
option(SLAP_BUILD_TESTS "Build tests for slap" ON)

# Code Coverage
option(SLAP_CODE_COVERAGE "Compile slap with Code Coverage." OFF)

# Documentation
option(SLAP_BUILD_DOCS "Build documentation for slap." OFF)

# Code Coverage
option(SLAP_CODE_COVERAGE "Compile slap with code coverage information." OFF)

# Enable clang-tidy analysis
option(SLAP_CLANG_TIDY "Run clang-tidy analyzer on the source code." OFF)

# Floating point precision
set(SLAP_FLOAT double CACHE STRING "Floating point precision for slap (float,double).")

# Build examples
option(SLAP_BUILD_EXAMPLES "Build examples for slap." OFF)

# Build with -march=native
option(SLAP_VECTORIZE "Compile with -march=native" OFF)

##############################
# Dependencies
##############################
if (SLAP_BUILD_DOCS)
  find_package(Doxygen REQUIRED)
endif ()
if (SLAP_BUILD_TESTS)
  # Google Test (Testing)
  CPMAddPackage(
      NAME googletest
      GITHUB_REPOSITORY google/googletest
      VERSION 1.13.0
      DOWNLOAD_EXTRACT_TIMESTAMP
      OPTIONS
      "INSTALL_GTEST OFF"
      "gtest_force_shared_crt ON"
  )
  add_library(gtest::gtest ALIAS gtest_main)
  enable_testing()
  include(GoogleTest)
  include(CTest)
endif ()

##############################
# Code Coverage
##############################
if (SLAP_CODE_COVERAGE AND CMAKE_C_COMPILER_ID MATCHES "GNU|Clang")
  message(STATUS "Compiling slap with coverage info.")
  add_compile_options(-O0 -g -fprofile-arcs -ftest-coverage)
  add_link_options(-fprofile-arcs -ftest-coverage)
endif ()

##############################
# Build
##############################

# Add compile options
if (NOT WIN32)
  add_compile_options(-Wall -Wextra -pedantic -Werror -Wno-error=unknown-pragmas)
  if (SLAP_VECTORIZE)
    add_compile_options(-march=native)
  endif()
endif()

## Make all includes relative to src/ directory
include_directories(${PROJECT_SOURCE_DIR}/src)

# Build source files
add_subdirectory(src/slap)

##############################
# Testing
##############################
if (SLAP_BUILD_TESTS)
  add_subdirectory(test)
endif ()

##############################
# Documentation
##############################
if (SLAP_BUILD_DOCS)
  add_subdirectory(docs)
endif ()

##############################
# Packaging / Installation
##############################
# add_subdirectory(packaging)


##############################
# Examples
##############################
if (SLAP_BUILD_EXAMPLES)
  add_executable(getting_started examples/getting_started.c)
  target_link_libraries(getting_started PUBLIC slap::slap)
  add_test(NAME getting_started COMMAND getting_started)

  add_subdirectory(examples/CPM_project)
  add_test(NAME fetchcontent_slap_example COMMAND fetchcontent_slap_example)

  add_subdirectory(examples/FetchContent_Project)
  add_test(NAME cpm_slap_example COMMAND cpm_slap_example)

  add_subdirectory(examples/Eigen_Comparison)
endif()
